//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

plugins {
    id 'com.github.davidmc24.gradle.plugin.avro-base' version '1.3.0'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
    id("com.github.ben-manes.versions") version "0.42.0"
}

allprojects {
    version = '0.7.8-SNAPSHOT'
    group = 'org.radarbase'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    // Configuration
    ext.githubRepoName = 'RADAR-base/RADAR-Schemas'
    ext.githubUrl = "https://github.com/${githubRepoName}.git"
    ext.issueUrl = "https://github.com/${githubRepoName}/issues"
    ext.website = 'https://radar-base.org'

    ext {
        avroVersion = '1.11.0'
        argparseVersion = '0.9.0'
        jacksonVersion = '2.13.2'
        radarJerseyVersion = "0.8.2"

        junitVersion = '4.13.2'
        confluentVersion = '7.0.1'
        kafkaVersion = '7.0.1-ce'
        okHttpVersion = '4.9.3'
        radarCommonsVersion = '0.13.2'
        slf4jVersion = '1.7.36'
        javaxValidationVersion = '2.0.1.Final'
        logbackVersion = '1.2.10'
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.fasterxml.jackson.module' && details.requested.name == 'jackson-module-kotlin' && details.requested.version == '2.13.2') {
                details.useVersion '2.13.1'
                details.because 'Fixes missing kotlin module of latest version'
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://packages.confluent.io/maven/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    // Generated avro files
    ext.avroOutputDir = file('src/generated/java')

    idea {
        module {
            downloadSources = true
        }
    }

    task downloadDependencies {
        description "Pre-downloads *most* dependencies"
        doLast {
            configurations.compileClasspath.files
            configurations.runtimeClasspath.files
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.hasProperty("ossrh.user") ? project.property("ossrh.user") : System.getenv("OSSRH_USER")
            password = project.hasProperty("ossrh.password") ? project.property("ossrh.password") : System.getenv("OSSRH_PASSWORD")
        }
    }
}

wrapper {
    gradleVersion '7.4.1'
}
