//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

plugins {
    id 'com.commercehub.gradle.plugin.avro-base' version '0.9.0'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.jfrog.artifactory' version '4.4.18'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.commercehub.gradle.plugin.avro-base'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    // Configuration
    version = '0.3.1-SNAPSHOT'
    group = 'org.radarcns'
    ext.githubRepoName = 'RADAR-CNS/RADAR-Schemas'

    targetCompatibility = '1.7'
    sourceCompatibility = '1.7'

    ext.avroVersion = '1.8.2'

    ext.githubUrl = "https://github.com/${githubRepoName}.git"
    ext.issueUrl = "https://github.com/${githubRepoName}/issues"
    ext.website = 'http://radar-cns.org'

    // dependencies
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    }

    dependencies {
        api (group: 'org.apache.avro', name: 'avro', version: avroVersion) {
            exclude group: 'org.xerial.snappy', module: 'snappy-java'
            exclude group: 'com.thoughtworks.paranamer', module: 'paranamer'
            exclude group: 'org.apache.commons', module: 'commons-compress'
            exclude group: 'org.tukaani', module: 'xz'
        }
    }

    // publishing
    ext.pomConfig = {
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'herkulano'
                name 'Herculano Campos'
                email 'herculano@goldenarm.com'
                organization 'Golden Arm Inc.'
            }
            developer {
                id 'fnobilia'
                name 'Francesco Nobilia'
                email 'francesco.nobilia@kcl.ac.uk'
                organization 'King\'s College London'
            }
            developer {
                id 'blootsvoets'
                name 'Joris Borgdorff'
                email 'joris@thehyve.nl'
                organization 'The Hyve'
            }
            developer {
                id 'strangepleasures'
                name 'Pavel Mikhailovskii'
                email 'pavel@thehyve.nl'
                organization 'The Hyve'
            }
        }
        issueManagement {
            system 'GitHub'
            url issueUrl
        }
        organization {
            name 'RADAR-CNS'
            url website
        }
        scm {
            connection 'scm:git:' + githubUrl
            url githubUrl
        }
    }
    ext.sharedManifest = manifest {
        attributes("Implementation-Title": rootProject.name,
                "Implementation-Version": version)
    }

    jar {
        manifest.from sharedManifest
    }
    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
        manifest.from sharedManifest
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
        manifest.from sharedManifest
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    // Generated avro files
    ext.avroOutputDir = file('src/generated/java')

    idea {
        module {
            downloadSources = true
        }
    }

    task downloadDependencies {
        description "Pre-downloads *most* dependencies"
        doLast {
            configurations.getAsMap().each { name, config ->
                println "Retrieving dependencies for $name"
                try {
                    config.files
                } catch (e) {
                    project.logger.info e.message // some cannot be resolved, silentlyish skip them
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '4.4'
}
