//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

plugins {
    id 'com.commercehub.gradle.plugin.avro-base' version '0.19.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.commercehub.gradle.plugin.avro-base'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    // Configuration
    version = '0.6.1-SNAPSHOT'
    group = 'org.radarbase'
    ext.githubRepoName = 'RADAR-base/RADAR-Schemas'

    ext.githubUrl = "https://github.com/${githubRepoName}.git"
    ext.issueUrl = "https://github.com/${githubRepoName}/issues"
    ext.website = 'https://radar-base.org'

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    ext {
        avroVersion = '1.9.2'
        argparseVersion = '0.8.1'
        jacksonVersion = '2.12.3'
        jacksonModuleVersion = '2.12.3'
        radarJerseyVersion = "0.6.0"

        junitVersion = '4.13.2'
        confluentVersion = '6.1.1'
        kafkaVersion = '6.1.1-ce'
        okHttpVersion = '4.9.1'
        radarCommonsVersion = '0.13.1'
        slf4jVersion = '1.7.30'
        javaxValidationVersion = '2.0.1.Final'
    }

    repositories {
        mavenCentral()
        maven { url 'https://packages.confluent.io/maven/' }
    }

    // dependencies
    dependencies {
        api (group: 'org.apache.avro', name: 'avro', version: avroVersion) {
            exclude group: 'org.xerial.snappy', module: 'snappy-java'
            exclude group: 'com.thoughtworks.paranamer', module: 'paranamer'
            exclude group: 'org.apache.commons', module: 'commons-compress'
            exclude group: 'org.tukaani', module: 'xz'
        }
    }

    // Generated avro files
    ext.avroOutputDir = file('src/generated/java')

    idea {
        module {
            downloadSources = true
        }
    }

    task downloadDependencies {
        description "Pre-downloads *most* dependencies"
        doLast {
            configurations.getAsMap().each { name, config ->
                println "Retrieving dependencies for $name"
                try {
                    config.files
                } catch (e) {
                    project.logger.info e.message // some cannot be resolved, silentlyish skip them
                }
            }
        }
    }
}

wrapper {
    gradleVersion '6.8.3'
}
