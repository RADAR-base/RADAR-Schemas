plugins {
    id 'application'
}

compileJava {
    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    runtimeOnly {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}

description = 'RADAR Schemas specification and validation tools.'

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation project(':radar-schemas-registration')
    implementation(platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion"))
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml")

    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: argparseVersion

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
}

application {
    mainClass.set('org.radarbase.schema.tools.CommandLineApp')
}

//---------------------------------------------------------------------------//
// Test definition                                                           //
//---------------------------------------------------------------------------//
test {
    inputs.dir "${project.rootDir}/../commons"
    inputs.dir "${project.rootDir}/../specifications"
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showExceptions = true
        showCauses = true
        showStackTraces = true
        showStandardStreams = true
    }
}

//---------------------------------------------------------------------------//
// Publishing                                                                //
//---------------------------------------------------------------------------//

task copyDependencies(type: Copy) {
    from(configurations.named("runtimeClasspath").map {it.files })
    into("$buildDir/third-party/")
    doLast {
        println("Copied third-party runtime dependencies")
    }
}

tasks.withType(Tar) {
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}
